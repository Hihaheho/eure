%start Swon
%title "SWON"
%grammar_type 'LL(k)'
%line_comment "#"

%on Quote, TypedQuote %enter Str
%on TextStart %enter Text

%scanner Str {
  %auto_newline_off
  %auto_ws_off
  %on Quote %enter INITIAL
}

%scanner Text {
  %auto_newline_off
  %auto_ws_off
  %on Newline %enter INITIAL
}

%%

Swon: { Binding } { Section } ;

Binding: Keys Bindings ;
  Bindings: ValueBinding | SectionBinding | TextBinding ;
  ValueBinding: Bind Value ;
  SectionBinding: Begin Swon End ;
  TextBinding: TextStart [Ws^] Text Newline ;

Section: At Keys { Binding } ;
  Keys: Key { Dot Key } ;
  Key: KeyBase [ ArrayMarker ] ;
  ArrayMarker: ArrayBegin [ Integer ] ArrayEnd ;
  KeyBase: Ident | ExtensionNameSpace | Str ;
  // extension name must always be valid ident, not quoted ident
  ExtensionNameSpace: Ext Ident ;

Value: Object | Array | Integer | Boolean | Null | StrContinues | TypedStr | Hole ;
  Object: Begin { Key Bind Value [ Comma ] } End ;
  Array: ArrayBegin { Value [ Comma ] } ArrayEnd ;
  Integer: /\d[\d_]*/ ;
  Boolean: True | False ;
  True: 'true' ;
  False: 'false';
  Null: 'null' ;
  Hole: '!' ;

StrContinues: Str { Continue Str } ;
Str: Quote InStr Quote ;
TypedStr: TypedQuote InStr Quote ;
  Quote: <INITIAL, Str>'"' ;
  TypedQuote: <INITIAL>/[^ \t\n\r\x00-\x1F\x22\x7F]+"/ ;

InStr: <Str>/(\\[nrt\\"0]|[^\\"\r\n])*/ ;
Text: <Text>/[^\\"\r\n]*/ ;

Newline: <Text>/\r\n|\r|\n/ ;
Ws: <Str, Text>/[\s--\r\n]+/ ;
At: '@' ;
Ext: '$' ;
Dot: '.' ;
Begin: '{' ;
End: '}' ;
ArrayBegin: '[' ;
ArrayEnd: ']' ;
Bind: '=' ;
Comma: ',' ;
Continue: '\\' ;
TextStart: ":" ;
Ident: /[^ \t\n\r\x00-\x1F\x22\x7F:$]+/ ;
