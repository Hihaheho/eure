%start Swon
%title "SWON"
%grammar_type 'LL(k)'
%line_comment "#"

%on Quote, TypedQuote %enter String
%on TextStart %enter Text

%scanner String {
  %auto_newline_off
  %auto_ws_off
  %on Quote %enter INITIAL
}

%scanner Text {
  %auto_newline_off
  %auto_ws_off
  %on Newline %enter INITIAL
}

%%

Swon: { Binding } { Section } ;

Binding: Keys Bindings ;
  Bindings: ValueBinding | SectionBinding | TextBinding ;
  ValueBinding: Bind Value ;
  SectionBinding: Begin Swon End ;
  TextBinding: TextStart [Ws^] Text Newline ;

Section: At Keys { Binding } ;
  Keys: Key { Dot Key } ;
  Key: KeyBase [ ArrayMarker ] ;
  ArrayMarker: ArrayBegin [ Integer ] ArrayEnd ;
  KeyBase: Ident | ExtensionNameSpace | String ;
  // extension name must always be valid ident, not quoted ident
  ExtensionNameSpace: Ext Ident ;

Value: Object | Array | Integer | Boolean | Null | StringContinues | TypedString | Hole ;
  Object: Begin { Key Bind Value [ Comma ] } End ;
  Array: ArrayBegin { Value [ Comma ] } ArrayEnd ;
  Integer: /\d[\d_]*/ ;
  Boolean: True | False ;
  True: 'true' ;
  False: 'false';
  Null: 'null' ;
  Hole: '!' ;

StringContinues: String { Continue String } ;
String: Quote InString Quote ;
TypedString: TypedQuote InString Quote ;
  Quote: <INITIAL, String>'"' ;
  TypedQuote: <INITIAL, String>/\p{XID_Start}\p{XID_Continue}*"/ ;

InString: <String>/(\\[nrt\\"0]|\p{Letter}|\p{Mark}|\p{Number}|[\p{Punctuation}--\\"]|\p{Symbol}|\p{Space_Separator})*/;
Text: <Text>/(\p{Letter}|\p{Mark}|\p{Number}|\p{Punctuation}|\p{Symbol}|\p{Space_Separator})*/;

Newline: <Text>/\r\n|\r|\n/ ;
Ws: <String, Text>/[\s--\r\n]+/ ;
At: '@' ;
Ext: '$' ;
Dot: '.' ;
Begin: '{' ;
End: '}' ;
ArrayBegin: '[' ;
ArrayEnd: ']' ;
Bind: '=' ;
Comma: ',' ;
Continue: '\\' ;
TextStart: ":" ;
Ident: /\p{XID_Start}\p{XID_Continue}*/ ;
